<?xml version="1.0" encoding="utf-8"?>
<!--
/**
 *  근태관리 - 연차관리 TabNavigator
 *
 *  @author     기은정(eunjung.ki@vroi.net)
 *  @version    13.07.2009
 *  @since      Flex 3.3.0 build 4589
 */
-->
<core:H5View 
    xmlns:mx="http://www.adobe.com/2006/mxml"
    xmlns:h5="http://www.h5.com/component"
    xmlns:core="http://www.h5.com/core"
    creationComplete="creationCompleteHandler()"
    codeArray="['DTM_PAY_WEEKDAY_CD']"
    width="500" height="300" styleName="panel1" >
    
    <mx:Script>
    <![CDATA[
    //--------------------------------------------------------------------------
    //
    //  Imports
    // 
    //--------------------------------------------------------------------------
    import h5.ria.flex.component.vo.ICodeKindVO;
    import h5.ria.flex.component.controls.Alert;
    import h5.ria.flex.component.utils.ClassUtil;
    import h5.ria.flex.component.utils.AlertUtil;
    import h5.ria.flex.component.utils.StringUtil;
    import h5.ria.flex.component.asset.AssetImage;
    import h5.ria.flex.core.dataset.DefaultDataSet;
    import h5.ria.flex.core.dataset.IRecord;
    import h5.ria.flex.core.dataset.IDataSet;
    import h5.ria.flex.core.dataset.DefaultDataSetList;
    import h5.ria.flex.core.dataset.IDataSetList;
    import h5.ria.flex.frame.events.ServiceDelegatorEvent;
    import h5.ria.flex.component.utils.DateUtil;
    
    import mx.utils.ObjectUtil;    
    
    //--------------------------------------------------------------------------
    //
    //  Variable
    //
    //--------------------------------------------------------------------------
    /**
     *  @private
     *  버튼 제어 
     */
    [Bindable]
    private var modifyButtonEnabled:Boolean = false;
    
    /** 
     * @private
     * 
     * main DataSet
     */          
    public var dsSearch:IDataSet;
    
    /**
     *  @private
     * 
     *  DataGrid 선택 index 
     */
    private var selIndex:int = 0;
    
    /** 
     * @private
     * 
     * 조회조건
     */          
    private var mainSearchValue:Object;
    
    /** 
     * @private
     * 
     * 외부에서 넘겨준조회조건
     */          
    private var _searchValue:Object;              
    
    public function set searchValue(value:Object):void
    {
        _searchValue = value;
        
    }
    [Bindable]
    public function get searchValue():Object
    {
        return _searchValue;    
    }
    
    //--------------------------------------------------------------------------
    //
    //  initialize
    //
    //--------------------------------------------------------------------------
    /**
     *  @private
     *  creationCompleteEventHandler
     */
    private function creationCompleteHandler():void
    { 
        var toDay:Date = new Date();
        var nextMonth:Number = Date.UTC(toDay.getFullYear(),toDay.getMonth()+1,1);
        
        base_ymd.startDate = DateUtil.dateToString(new Date(), "YYYYMM") + "01";
        base_ymd.endDate = DateUtil.convertToDateString( new Date(nextMonth-(1000*60*60*24)) );
    }
    
    //-------------------------------------------------------------------------- 
    //
    //  Override Methods
    //
    //--------------------------------------------------------------------------
    /**
     *  @override
     *  코드 조회 완료후 이벤트
     */
    override protected function afterCreation(event:Event):void
    {
        
        var voWeekCode:ICodeKindVO = ClassUtil.codeKindVOInstace(); 
        voWeekCode.dataProvider = this.getCodesByKind('DTM_PAY_WEEKDAY_CD');
        colWeekCode.codeKind = voWeekCode;
    }
    
    /**
     *  @override
     *  WebService호출
     *  @param   actionType   호출종류
     *  @param   value        인자값
     */ 
    override protected function doAction(actionType:String, value:Object):void
    {  
        // 서버측으로 전달되어질 데이터 구조
        var sendDataList:IDataSetList = new DefaultDataSetList();
        var ds_Condition:IDataSet;
        var record:IRecord;
         
        switch (actionType)
        {
            case "search": // 조회
            
                ds_Condition = new DefaultDataSet("ds_C_Data");
                ds_Condition.addColumn("dtm_environment_id");           
                ds_Condition.addColumn("company_cd");           
                ds_Condition.addColumn("sta_ymd");           
                ds_Condition.addColumn("end_ymd");         
                
                record = ds_Condition.newRecord();
                record.setValueOf("dtm_environment_id",value.dtm_environment_id);   
                record.setValueOf("company_cd",value.company_cd);   
                record.setValueOf("sta_ymd",value.sta_ymd);   
                record.setValueOf("end_ymd",value.end_ymd);   
                
                sendDataList.addDataSet("ds_C_Data",ds_Condition);
                
                this.executeTransaction("DTM_010_R04", sendDataList, actionType);
                
                break;
            
            case "save": // 저장
                
                for (var i:int = 0; i < dsSearch.getRecords().length; i++)
                {
                    if (dsSearch.getRecordAt(i).getValueByName("sStatus") == 'U')
                    {
                        if (StringUtil.emptyOrNull(dsSearch.getRecordAt(i).getValueByName("dtm_schedule_id")))
                        {
                            dsSearch.getRecordAt(i).setValueOf('dtm_schedule_id','dtm_schedule_id_'+i);
                            dsSearch.getRecordAt(i).setValueOf('sStatus','I');
                        }
                    }
                }
                
                ds_Condition = new DefaultDataSet("ds_D_Data");
                dsSearch.sendOnlyDirtyRecord(true);
                sendDataList.addDataSet("ds_D_Data",dsSearch);
                
                this.executeTransaction("DTM_010_S05", sendDataList, actionType);
                
                /*
                if (adg.validateAll())
                {
                    ds_Condition = new DefaultDataSet("ds_D_Data");
                    dsSearch.sendOnlyDirtyRecord(true);
                    sendDataList.addDataSet("ds_D_Data",dsSearch);
                    
                    this.executeTransaction("DTM_010_S05", sendDataList, actionType);
                }
                */
                
                break;
        }
    }
    
    
    
    //--------------------------------------------------------------------------
    //
    //  ResultEvent
    //
    //--------------------------------------------------------------------------
    /**
     *  @private
     *  평가정의 조회 Result result
     */
    override protected function result(event:ServiceDelegatorEvent):void
    {
        var result:IDataSetList = event.result as IDataSetList;
        var eType:String = event.actionType;
        
        switch(eType){
            case "search" :     // 조회 후 처리
                
                dsSearch = result.getDataSet("ds_D_Data")
                adg.dataProvider = dsSearch.getRecords();
                modifyButtonEnabled = true;
                
                if (!IDataSet(result.getDataSet("ds_D_Data")).getRecords().length
                    || !IDataSet(result.getDataSet("ds_D_Data")))
                {
                    Alert.error(getTerms('ALERT_RETIEVE_NULL1'));
                } 
                else
                {
                    adg.selectedIndex = selIndex;
                }
                
                break; 
                
            case "save" :
            
                Alert.info(getTerms('ALERT_SAVE_OK1'), saveCompleteHandler);
                
                break;
        }
        
    }
    
    //--------------------------------------------------------------------------
    //
    //  FaultEvent
    //
    //--------------------------------------------------------------------------
    /**
     *  @private
     *  Fault
     */
    override protected function fault(event:ServiceDelegatorEvent):void
    {
         var eType:String = event.actionType;
        var target:String; 
        
        switch(eType)
        {
            case "search" :
                
                target = "[조회]";
                break;
                
            case "save" :
            
                target = "[저장]";
                break;
        }
        
        if (!StringUtil.emptyOrNull(event.result.faultCode))
        {
            Alert.error(target + getTerms(event.result.faultCode));
        }
        else
        {
            Alert.error(target + getTerms('ALERT_WORK_FAIL1'));
        }
    }
    
    //--------------------------------------------------------------------------
    //
    //  Event Handlers
    //
    //--------------------------------------------------------------------------
    /**
     *  @private
     * 
     *  조회 시점에 저장되지 않는 데이터가 존재할경우 AlertHandler
     */
    private function alertHandler(event:Object):void
    {
        // YES
        if(event.detail == Alert.YES)
        {
            selIndex = 0;
            setSearchValue();
            doAction("search", mainSearchValue);
        }
    }
    
    /**
     *  @private
     * 
     *  저장확인  AlertHandler
     */
    private function saveAlertHandler(event:Object):void
    {
        if(event.detail == Alert.YES)
        {
            doAction("save", null);
        }
    }
    
    /**
     *  @private
     * 
     *  저장완료 AlertHandler
     */
    private function saveCompleteHandler(event:Object):void
    {
        doAction("search", mainSearchValue);
    }
    
    //--------------------------------------------------------------------------
    //
    //  Methods
    //
    //--------------------------------------------------------------------------
    /**
     *  @private
     *  초기화 (외부에서 접근가능)
     */
    public function outerReset():void
    {
        adg.dataProvider = null;
    }
    
    /**
     *  @private
     *  박스 활설/비활성화 (외부에서 접근가능)
     */
    public function outerBoxEnable(value:Boolean):void
    {
        this.enabled = value;
        modifyButtonEnabled = false;
    }
    
    /**
     *  @private
     * 
     *  조회 전 수정데이터 확인.
     */
    private function beforeSearch():void
    {
        if (dsSearch)
        {
            if (dsSearch.checkDirty())
            {
                Alert.confirm('[달력관리내역]' + getTerms('CONFIRM_WORK_CONTINUE1'), alertHandler);
                return;
            }
        }
        selIndex = 0;
        setSearchValue();
        doAction("search", mainSearchValue);            
        
    }
    
    /**
     *  @private
     * 
     *  저장 전  확인.
     */
    private function beforeSave():void
    {
        if (dsSearch)
        {
            if (!dsSearch.checkDirty())
            {
                Alert.error(getTerms("ALERT_NO_SAVE_DATA1"));
                return;
            }
        }
                    
        Alert.confirm(getTerms('CONFIRM_SAVE1'), saveAlertHandler);
    }
    
    /**
     *  @private
     * 
     *  조회조건 설정
     */
    private function setSearchValue():void
    {
        mainSearchValue = new Object();
        mainSearchValue.dtm_environment_id = searchValue.dtm_environment_id;
        mainSearchValue.company_cd      = getCompanyCode();
        mainSearchValue.sta_ymd         = base_ymd.startDate;
        mainSearchValue.end_ymd         = base_ymd.endDate;
        
    }   
    
    ]]>
    </mx:Script>
    
        
    <!-- 조회영역 -->
    <h5:VBox width="100%" verticalGroupBoxMotion="true" styleName="Box1">
        <h5:HBox width="100%" styleName="SearchBox">
            <h5:Grid width="100%" styleName="searchGrid">
                <h5:GridRow width="100%" styleName="searchGridRow">
                    <h5:GridItem styleName="searchGridItemLabel">
                        <h5:Label text="기간" labelCode="PERIOD1" icon="requirement"/>
                    </h5:GridItem>
                    <h5:GridItem styleName="searchGridItem">
                        <h5:Date2Date id="base_ymd" showEndDate="true" />
                    </h5:GridItem>
                    
                    <!--조회버튼-->
                    <h5:GridItem width="100%" styleName="searchButtonGridItem">
                        <h5:Button label="조회" labelCode="BTN_RETRIEVE1" click="{beforeSearch()}" styleName="searchButton" />
                    </h5:GridItem>
                </h5:GridRow>
            </h5:Grid>
        </h5:HBox>
    </h5:VBox>
     
    <h5:VBox width="100%" height="100%" styleName="groupBox" >
        <h5:HBox width="100%" styleName="titleBox1">
            <mx:Image source="{AssetImage.IMG_BUTTET1}"/>
            <h5:Label text="달력관리" labelCode="CALENDAR_MNG1"/>
            <mx:Spacer width="10" />
            <h5:Label id="searchTitle" color="black"/>
            <mx:Spacer width="100%" />
            <h5:Button label="저장" labelCode="BTN_SAVE1" click="{beforeSave()}" enabled="{modifyButtonEnabled}" styleName="saveButton"/>
        </h5:HBox>
        <h5:AdvancedDataGrid id="adg" width="100%" height="100%" editable="true" sortExpertMode="true"
            showStatusColumn="false" showDeleteColumn="false" styleName="DataGridTypeA">
            <h5:columns>
                <h5:AdvancedDataGridColumn labelCode="YMD1" headerText="일자" dataField="ymd" format="date" width="100"/>
                <h5:AdvancedDataGridColumn labelCode="WEEk1" headerText="요일" dataField="week" width="100"/>
                <h5:AdvancedDataGridColumn labelCode="MINUS_DD1" headerText="감산일수" dataField="minus_dd" textAlign="right" visible="false"/>
                <h5:AdvancedDataGridColumn labelCode="MOON_YMD1" headerText="음력일자" dataField="moon_ymd" format="date" visible="false"/>
                <h5:AdvancedDataGridColumn labelCode="PAY_WEEK1" headerText="급여요일" dataField="week_cd" id="colWeekCode" isRequired="true" textAlign="left" width="90"/>
                <h5:AdvancedDataGridColumn labelCode="PRINT_NM1" headerText="출력명" dataField="note" editable="true" textAlign="left" width="300">
                    <h5:itemEditor>
                        <mx:Component>
                            <h5:TextInputRenderer maxBytes="50" textAlign="left" imeMode="KOREAN"/>
                        </mx:Component>
                    </h5:itemEditor>
                </h5:AdvancedDataGridColumn>
            </h5:columns>
        </h5:AdvancedDataGrid>
    </h5:VBox>    
</core:H5View>